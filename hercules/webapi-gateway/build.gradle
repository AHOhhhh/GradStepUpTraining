buildscript {
    ext {
        springBootVersion = '1.5.8.RELEASE'
        springCloudVersion = 'Dalston.SR4'
        artifactRepoBase = "http://nexus.hercules.fun/repository"
        mavenPublic = "${artifactRepoBase}/maven-public/"
    }
    repositories {
        maven {
            url mavenPublic
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven-publish'
apply plugin: "idea"
apply plugin: "eclipse"
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: "jacoco"


group = 'fun.hercules'
version = '1.0.0'
if (!project.hasProperty("release")) {
    version = "${version}-${project.hasProperty("buildVersion") ? project.property("buildVersion") : "SNAPSHOT"}"
}


sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {
        url mavenPublic
    }
}

publishing {
    publications {
        buildResult(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "${artifactRepoBase}/${project.version.endsWith('-SNAPSHOT') ? 'maven-snapshots' : 'maven-releases'}/"
            credentials { username 'admin'; password 'admin123' }
        }
    }
}


jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

task dev {
    doLast {
        bootRun.systemProperty 'spring.profiles.active', 'dev'
    }
}

dependencies {
    compile('org.springframework.cloud:spring-cloud-starter-zuul')
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.cloud:spring-cloud-starter-feign')
    compile("io.jsonwebtoken:jjwt:0.9.0")
    compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.6.6')
    compile 'com.puppycrawl.tools:checkstyle:8.4'
    compile 'net.sourceforge.pmd:pmd:5.8.1'
    compile 'com.google.code.findbugs:findbugs:3.0.1'

    compile 'io.springfox:springfox-swagger2:2.7.0'
    compile 'io.springfox:springfox-swagger-ui:2.7.0'

    compile 'com.google.guava:guava:23.0'
    compile 'org.apache.commons:commons-lang3:3.6'
    compileOnly('org.projectlombok:lombok')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.security:spring-security-test')

}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}
